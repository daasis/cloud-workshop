buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.1.RELEASE'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

subprojects {
    apply plugin: 'war'
    apply plugin: 'groovy'
    apply plugin: 'spring-boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'docker'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'no.finn.workshop.cloud'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile("org.codehaus.groovy:groovy")

        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    task buildDocker(type: Docker, dependsOn: build) {
        applicationName = war.baseName
        dockerfile = file('src/main/docker/Dockerfile')
        doFirst {
            copy {
                from war
                into stageDir
            }
        }
    }
}
